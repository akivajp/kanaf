//
// セーブ画面
//

#sub_save
// セーブ画面を呼び出す
[set event="on_right_down" jump="#save_end"]
[map layer="top.save" create show activate top]
[map image="bg_save" cx=0 cy=0 alpha=200]
[map image="menu_prev" hover_image="menu_prev_hover"
 rx=-50 y=30 jump="#save_end"]

// ログ番号1
[call target="#sub_draw_slot" id=1 lx=80 y=85]
// ログ番号2
[call target="#sub_draw_slot" id=2 lx=80 y=210]
// ログ番号3
[call target="#sub_draw_slot" id=3 lx=80 y=335]
[s]

#save_end
// セーブ画面終了
[map layer="top.save" delete]
[return]

#sub_draw_slot
// データスロットを描画
[[
  args.lx = args.lx or 0
  args.y = args.y or 0
  args.font_size = args.font_size or 26
]]
[if cond=(kanaf.get_log(args.id))]
  [map image="frame_base" hover_image="frame_hover"
   lx=(args.lx) y=(args.y) call="#sub_save_dialog" save_id=(args.id)]
  [map image=(kanaf.get_log_image(args.id))
   lx=(args.lx + 10) y=(args.y + 12)]
  [map text=(kanaf.get_log_scene(args.id))
   lx=(args.lx + 160) y=(args.y + 20) font_size=(args.font_size)]
  [map text=(kanaf.get_log_date(args.id))
   lx=(args.lx + 160) y=(args.y + 70) font_size=(args.font_size)]
[else]
  [map image="frame_base" hover_image="frame_hover"
   lx=(args.lx) y=(args.y) call="#sub_save_dialog" save_id=(args.id)]
[endif]
[return]

#sub_save_dialog
[set event="on_right_down" jump="#menu_no"]
[map layer="top.dialog" create show activate]
[map image="bg_dialog" cx=0 cy=0 alpha=200]
[map image="menu_on_save" cx=0 cy=-75]
[map image="menu_yes" hover_image="menu_yes_hover"
 cx=-100 cy=50 jump="#menu_yes"]
[map image="menu_no" hover_image="menu_no_hover"
 cx=100 cy=50 jump="#menu_no"]
[s]

#menu_yes
[map layer="top.dialog" delete]
//[debug msg="start saving"]
[save log id=(args.save_id)]
//[debug msg="end saving"]
[jump target="#sub_save"]

#menu_no
[map layer="top.dialog" delete]
[return]

